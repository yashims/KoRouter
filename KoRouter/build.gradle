plugins {
    id 'com.android.library'
//    id 'kotlin-multiplatform'
    id 'org.jetbrains.kotlin.multiplatform'
    id 'org.jetbrains.kotlin.native.cocoapods'
    id 'com.jfrog.bintray'
}

group = "dev.yashims"
version = "0.0.1"
archivesBaseName = 'korouter'


ext {
    library_build = 1
    library_name = group + ".korouter"
}

android {
    compileSdkVersion 28
    archivesBaseName = library_name

    defaultConfig {
        minSdkVersion 19
    }

    sourceSets {
        main {
            def root = "src/androidMain"
            setRoot(root)
            java.srcDirs += "${root}/kotlin"
        }
    }
}

dependencies {
//    implementation project(':util')
    // Specify Kotlin/JVM stdlib dependency.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

import org.jetbrains.kotlin.gradle.tasks.FatFrameworkTask

kotlin {
    cocoapods {
        // Configure fields required by CocoaPods.
        summary = "One of presentation layer routing module made by Kotlin/Native."
        homepage = "https://github.com/yashims/KoRouter"
        license = "MIT"
        authors = "yashims85@gmail.com"
    }

    android()
    android {
        // releaseとdebugのaarを作る
        publishLibraryVariants("release", "debug")
    }

    iosArm64('ios') {
        binaries {
//            framework()
        }
    }

    iosX64() {
        binaries {
//            framework()
        }
    }

    // Create a task building a fat framework.
    task debugFatFramework(type: FatFrameworkTask) {
        // The fat framework must have the same base name as the initial frameworks.
        baseName = "korouter"

        // The default destination directory is '<build directory>/fat-framework'.
        destinationDir = file("$buildDir/fat-framework/debug")

        // Specify the frameworks to be merged.
        from(
                targets.ios.binaries.getFramework("DEBUG"),
                targets.iosX64.binaries.getFramework("DEBUG")
        )
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation project(':util')
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
            }
        }

        androidMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
            }
        }

        iosMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutines_version"
            }
        }

        iosX64Main.dependsOn(iosMain)

        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

// This task attaches native framework built from ios module to Xcode project
// (see iosApp directory). Don't run this task directly,
// Xcode runs this task itself during its build process.
// Before opening the project from iosApp directory in Xcode,
// make sure all Gradle infrastructure exists (gradle.wrapper, gradlew).
task copyFramework {
    def buildType = project.findProperty('kotlin.build.type') ?: 'DEBUG'
    def target = project.findProperty('kotlin.target') ?: 'ios'
    dependsOn debugFatFramework

    doLast {
        def srcDir = debugFatFramework.destinationDir.toString()
        def targetDir = getProperty('configuration.build.dir')
        copy {
            from srcDir
            into targetDir
            include 'korouter.framework/**'
            include 'korouter.framework.dSYM'
        }
    }
}

/**
 * Jcenter deployment
 */
def libraryId = 'korouter'
def siteUrl = 'https://github.com/yashims/KoRouter'
def githubUrl = 'https://github.com/yashims/KoRouter'
bintray {

    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_key') ? bintray_key : ''
    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = libraryId
        licenses = ['MIT']
        websiteUrl = siteUrl
        issueTrackerUrl = githubUrl + '/blob/master/LICENSE'
        vcsUrl = githubUrl + '.git'
        issueTrackerUrl = githubUrl + '/issues'
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}

